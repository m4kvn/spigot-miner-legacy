plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.7.10'
}

group = "com.MasahiroSaito.Spigot"
version = "v2.0-1.19-R0.1"

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

configurations {
    embed
    compileOnly.extendsFrom(embed)
}

dependencies {
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])
    compileOnly 'org.spigotmc:spigot-api:1.19-R0.1-SNAPSHOT'
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.7.10"
    embed "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.7.10"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

jar {
    exclude "META-INF/versions/9/module-info.class"
    from configurations.embed.collect {
        it.isDirectory() ? it : zipTree(it)
    }
}

task copyJarToServer(type: Copy) {
    from jar
    into "server/plugins"
}
build.dependsOn copyJarToServer